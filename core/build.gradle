plugins {
    id 'java'
    id 'maven-publish'
}

archivesBaseName = "geckolib-core"

repositories {
    mavenCentral()
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
}

sourcesJar {
    //Include the license into the sourcesJar
    from(rootProject.file("LICENSE"))
}

dependencies {
    implementation "com.google.code.gson:gson:${gson_version}"
    implementation "it.unimi.dsi:fastutil:${fastutil_version}"
}

jar {
    from(rootProject.file("LICENSE"))
    manifest {
        attributes([
                'Specification-Title'     : project_name,
                'Specification-Vendor'    : authors,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project_name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = archivesBaseName
            version = version

            from components.java
        }
    }
    repositories {
        maven {
            name =  "AlpineCloud"
            url = "https://lib.alpn.cloud/alpine-public"
            credentials {
                username = System.getenv("ALPINE_MAVEN_NAME")
                password = System.getenv("ALPINE_MAVEN_SECRET")
            }
        }
    }
}